# Reusable UI components for the docs site

fn head_meta(page_title: String) -> Html:
  return head():
    meta(charset="utf-8")
    meta(name="viewport", content="width=device-width, initial-scale=1")
    meta(name="description", content="FUSE language documentation - specs, runtime, and API reference")
    title():
      html.text(page_title)
    link({"rel": "stylesheet", "href": asset("site/assets/css/styles.css")})

fn nav_link(href: String, label: String, is_active: Bool) -> Html:
  var class_name = "nav-link"
  if is_active: class_name = "nav-link is-active"
  return a({"href": href, "class": class_name}):
    html.text(label)

fn sidebar_link(href: String, label: String, is_active: Bool) -> Html:
  var class_name = "sidebar-link"
  if is_active:
    class_name = "sidebar-link is-active"
  return a({"href": href, "class": class_name}):
    html.text(label)

fn sidebar_toggle(show_sidebar: Bool) -> Html:
  if show_sidebar:
    return button(
      class="sidebar-toggle"
      id="sidebar-toggle"
      type="button"
      aria_label="Toggle navigation"
      aria_expanded="false"
    ):
      span(class="burger-line")
      span(class="burger-line")
      span(class="burger-line")
  return html.raw("")

fn topbar(current_section: String, show_sidebar: Bool) -> Html:
  return header(class="topbar"):
    div(class="topbar-inner"):
      a(href="/", class="brand"):
        svg.inline("docs-mark")
        span(): "FUSE"
      nav({"class": "topnav", "aria-label": "Main navigation"}):
        nav_link("/specs/fuse", "Specs", current_section == "specs")
        nav_link("/openapi", "API", current_section == "openapi")
      sidebar_toggle(show_sidebar)

fn spec_sidebar(active_slug: String) -> Html:
  return aside({"class": "sidebar", "id": "sidebar", "aria-label": "Specs navigation"}):
    nav(class="sidebar-nav"):
      sidebar_link("/specs/fuse", "Language", active_slug == "fuse")
      sidebar_link("/specs/fls", "Specification", active_slug == "fls")
      sidebar_link("/specs/runtime", "Runtime", active_slug == "runtime")
      sidebar_link("/specs/scope", "Scope", active_slug == "scope")
    div(class="toc", id="toc")

fn maybe_sidebar(show_sidebar: Bool, active_slug: String) -> Html:
  if show_sidebar:
    return spec_sidebar(active_slug)
  return html.raw("")

fn page_shell(
  page_title: String,
  current_section: String,
  active_slug: String,
  show_sidebar: Bool,
  content: Html
) -> Html:
  var grid_class = "content-grid"
  if show_sidebar:
    grid_class = "content-grid has-sidebar"
  return html(lang="en"):
    head_meta(page_title)
    body():
      div(class="app-shell"):
        topbar(current_section, show_sidebar)
        div({"class": grid_class}):
          maybe_sidebar(show_sidebar, active_slug)
          button(
            class="panel-overlay"
            id="panel-overlay"
            type="button"
            aria_label="Close navigation"
            hidden="hidden"
          )
          main({"class": "panel", "id": "content"}):
            content
      script({"src": asset("site/assets/js/libs/highlight_11.11.1.min.js")})
      script({"type": "module", "src": asset("site/assets/js/main.js")})

fn spec_page(slug: String, label: String) -> Html:
  let content = article({"class": "spec-content", "data-spec": slug}):
    div(class="loading-state"):
      html.text("Loading ${label}...")
  return page_shell("${label} - FUSE Docs", "specs", slug, true, content)

fn openapi_page() -> Html:
  let content = article(class="openapi-content", data_view="openapi"):
    div(class="loading-state"): "Loading API documentation..."
  return page_shell("API - FUSE Docs", "openapi", "", false, content)

fn home_page() -> Html:
  let content = article(class="home-content"):
    section(class="hero"):
      h1(): "FUSE Documentation"
      p(class="lead"): "Write intent. Get software."
      p(class="subtitle"): "A small, strict, \"default-sane\" language for CLI apps and HTTP services."
    section(class="quick-links"):
      h2(): "Get Started"
      div(class="card-grid"):
        a(href="/specs/fuse", class="card"):
          h3(): "Language Guide"
          p(): "Learn the core concepts, syntax, and features of FUSE."
        a(href="/specs/fls", class="card"):
          h3(): "Specification"
          p(): "Formal language specification with grammar and type system details."
        a(href="/specs/runtime", class="card"):
          h3(): "Runtime"
          p(): "Built-ins, database access, HTTP binding, and concurrency."
        a(href="/openapi", class="card"):
          h3(): "API Reference"
          p(): "Interactive OpenAPI documentation for the docs service."
  return page_shell("FUSE Docs", "home", "", false, content)
